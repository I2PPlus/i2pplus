task prepUpdateRouter(type: Copy) {
    dependsOn ':core:jar', ':router:jar'
    // Pass in paths as a closure, so they are not executed during configuration
    from {[
        project(':core').jar.archiveFile,
        project(':router').jar.archiveFile
    ]}
    into 'pkg-temp/lib'
}

task prepUpdateSmall(type: Copy) {
    dependsOn prepUpdateRouter
    dependsOn ':apps:ministreaming:jar', ':apps:streaming:jar'
    dependsOn ':apps:routerconsole:jar', ':apps:i2ptunnel:i2ptunnelJar'
    dependsOn ':apps:routerconsole:war', ':apps:i2ptunnel:war'
    dependsOn ':apps:addressbook:jar'
    // Base dir
    into 'pkg-temp'
    into('lib') {
        from {[
            project(':apps:ministreaming').jar.archiveFile,
            project(':apps:streaming').jar.archiveFile,
            project(':apps:routerconsole').jar.archiveFile,
            project(':apps:i2ptunnel').i2ptunnelJar.archiveFile,
            project(':apps:jrobin').jar.archiveFile,
        ]}
    }
    into('webapps') {
        from {[
            project(':apps:routerconsole').war.archiveFile,
            project(':apps:i2ptunnel').war.archiveFile,
        ]}
    }
}

task prepUpdate(type: Copy) {
    dependsOn ':apps:sam:jar'
    dependsOn ':apps:i2psnark:i2psnarkJar', ':apps:i2psnark:jar'
    dependsOn ':apps:systray:jar'
    dependsOn ':apps:jetty:jar'
    dependsOn ':apps:desktopgui:jar'
    dependsOn ':apps:susidns:war', ':apps:susimail:war'
    dependsOn ':apps:i2psnark:war'
    dependsOn ':apps:i2pcontrol:war'
    dependsOn ':apps:imagegen:war'
    inputs.files fileTree('pkg-temp/lib').include('*.jar')
    inputs.files fileTree('pkg-temp/webapps').include('*.war')
    from 'pkg-temp'
    mustRunAfter ':apps:susidns:war', ':apps:susimail:war'
    mustRunAfter ':apps:i2psnark:war'
    mustRunAfter ':apps:i2pcontrol:war'
    mustRunAfter ':apps:imagegen:war'
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    // Base dir
    into 'pkg-temp'
    into('lib') {
        from {[
            project(':apps:sam').jar.archiveFile,
            project(':apps:i2psnark').i2psnarkJar.archiveFile,
            project(':apps:systray').jar.archiveFile,
            project(':apps:desktopgui').jar.archiveFile,
            project(':apps:jetty').jar.archiveFile,
        ]}
        // as of 0.7.12; someday, we can remove these from the updater
        from 'apps/susidns/src/WEB-INF/lib/jstl.jar'
        from 'apps/susidns/src/WEB-INF/lib/standard.jar'
    }
    into('webapps') {
        from {[
            project(':apps:susidns').war.archiveFile,
            project(':apps:susimail').war.archiveFile,
            project(':apps:i2psnark').war.archiveFile,
            project(':apps:i2pcontrol').war.archiveFile,
            project(':apps:imagegen').war.archiveFile,
        ]}
    }
    from('history.txt') {
        filter(org.apache.tools.ant.filters.HeadFilter, lines:1500)
    }
    doLast {
        String more = '\n\n----------------\n\nEARLIER HISTORY IS AVAILABLE IN THE SOURCE PACKAGE'
        ant.concat(more, append: 'true', destfile: 'pkg-temp/history.txt')
    }
    from 'LICENSE.txt'
    into('licenses') { from 'licenses' }
    from 'installer/resources/blocklist.txt'
    from 'installer/resources/deletelist.txt'
    into('certificates') { from 'installer/resources/certificates' }
    into('locale') { from 'installer/resources/locale' }
    into('man') { from 'installer/resources/man' }
}

task updaterRouter(type: Zip) {
    dependsOn prepUpdateRouter
    // https://stackoverflow.com/questions/54501697/gradle-war-plugin-how-to-change-name-of-an-archive
    // https://discuss.gradle.org/t/destinationdir-is-deprecated-archivename-is-deprecated/31586
    archiveFileName.set('i2pupdate.zip')
    destinationDirectory.set(file('dist'))
    from 'pkg-temp'
}

task updaterSmall(type: Zip) {
    dependsOn prepUpdateSmall
    archiveFileName.set('i2pupdate.zip')
    destinationDirectory.set(file('dist'))
    from 'pkg-temp'
}

task updater(type: Zip) {
    dependsOn prepUpdate
    archiveFileName.set('i2pupdate.zip')
    destinationDirectory.set(file('dist'))
    from 'pkg-temp'
}
