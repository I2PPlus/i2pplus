apply plugin: 'java'
apply plugin: 'java-library'

String getReleaseVersion() {
    def releaseVersion
    file("core/java/src/net/i2p/CoreVersion.java").readLines().findAll({ line ->
        line.contains("public final static String VERSION")
    }).first().eachMatch('.*"([^"]+)";', {
        releaseVersion = it[1]
    })
    releaseVersion
}

String getBuildVersion() {
    def buildVersion
    file("router/java/src/net/i2p/router/RouterVersion.java").readLines().findAll({ line ->
        line.contains("public final static long BUILD")
    }).first().eachMatch('.*=\\s+([0-9]+);', {
        buildVersion = it[1]
    })
    buildVersion
}

String getBuildExtra() {
    def buildExtra
    file("router/java/src/net/i2p/router/RouterVersion.java").readLines().findAll({ line ->
        line.contains("public final static String EXTRA")
    }).first().eachMatch('.*"(.*)";', {
        buildExtra = it[1]
    })
    buildExtra
}

String getBuiltBy() {
    def builtBy = ""
    def overrideProps = file("override.properties")
    if (overrideProps.exists()) {
        overrideProps.readLines().findAll({ line ->
            line.contains("build.built-by")
        }).first().eachMatch('.*=(.*)', {
            builtBy = it[1]
        })
    }
    builtBy
}

String compat(String src) {
    if (src.contains('.')) {
        src.substring(src.lastIndexOf('.') + 1)
    } else {
        src
    }
}

String getWorkspaceVersion() {
    "git" // TODO: extract revision
}

def releaseVersion = getReleaseVersion()
def buildVersion = getBuildVersion()
def buildExtra = getBuildExtra()
def fullVersion = "$releaseVersion-$buildVersion$buildExtra"

def builtBy = getBuiltBy()
def workspaceVersion = getWorkspaceVersion()

subprojects {
    // Only apply to actual projects, not container directories
    if (project.name == 'apps') {
        return
    }
    
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    dependencies {
        // https://stackoverflow.com/questions/44493378/whats-the-difference-between-api-api-and-compile-in-gradle
        testImplementation 'junit:junit:4.+'
        testImplementation 'org.hamcrest:hamcrest-library:1.3'
        testImplementation 'org.mockito:mockito-core:4.11.0'
    }
}

task codeCoverageReport(type: JacocoReport) {
    // Skip this task if no Java projects are found
    doFirst {
        def javaProjects = subprojects.findAll { it.name != 'apps' && it.hasProperty('java') }
        if (javaProjects.isEmpty()) {
            println "No Java projects found for coverage report"
            return
        }
    }
}

apply from: file('gradle/update.gradle')
