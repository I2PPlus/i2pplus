String getReleaseVersion() {
    def releaseVersion
    file("core/java/src/net/i2p/CoreVersion.java").readLines().findAll({ line ->
        line.contains("public final static String VERSION")
    }).first().eachMatch('.*"([^"]+)";', {
        releaseVersion = it[1]
    })
    releaseVersion
}

String getBuildVersion() {
    def buildVersion
    file("router/java/src/net/i2p/router/RouterVersion.java").readLines().findAll({ line ->
        line.contains("public final static long BUILD")
    }).first().eachMatch('.*=\\s+([0-9]+);', {
        buildVersion = it[1]
    })
    buildVersion
}

String getBuildExtra() {
    def buildExtra
    file("router/java/src/net/i2p/router/RouterVersion.java").readLines().findAll({ line ->
        line.contains("public final static String EXTRA")
    }).first().eachMatch('.*"(.*)";', {
        buildExtra = it[1]
    })
    buildExtra
}

String getBuiltBy() {
    def builtBy = ""
    def overrideProps = file("override.properties")
    if (overrideProps.exists()) {
        overrideProps.readLines().findAll({ line ->
            line.contains("build.built-by")
        }).first().eachMatch('.*=(.*)', {
            builtBy = it[1]
        })
    }
    builtBy
}

String compat(String src) {
    if (src.contains('.')) {
        src.substring(src.lastIndexOf('.') + 1)
    } else {
        src
    }
}

String getWorkspaceVersion() {
    "git" // TODO: extract revision
}

def releaseVersion = getReleaseVersion()
def buildVersion = getBuildVersion()
def buildExtra = getBuildExtra()
def fullVersion = "$releaseVersion-$buildVersion$buildExtra"

def builtBy = getBuiltBy()
def workspaceVersion = getWorkspaceVersion()

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    dependencies {
        // https://stackoverflow.com/questions/44493378/whats-the-difference-between-api-api-and-compile-in-gradle
        testImplementation 'junit:junit:4.+'
        testImplementation 'org.hamcrest:hamcrest-library:1.3'
        testImplementation 'org.mockito:mockito-core:4.11.0'
    }

    java {
        sourceCompatibility = 21
        targetCompatibility = 21
        manifest {
            attributes(
                'Specification-Title': '',
                'Specification-Version': "$releaseVersion",
                'Specification-Vendor': 'The I2P Project https://geti2p.net/',
                'Implementation-Title': '',
                'Implementation-Version': "$fullVersion",
                'Implementation-Vendor': 'I2P+ https://i2pplus.github.io/',
                'Built-By': "$builtBy",
                'Build-Date': 'reproducible',
                'Base-Revision': "$workspaceVersion",
                'Workspace-Changes': '',
                'X-Compile-Source-JDK': "$sourceCompatibility",
                'X-Compile-Target-JDK': "$targetCompatibility"
            )
        }
    }

    // Enable Xlint warnings for detailed compilation reports (excluding problematic ones)
    compileJava.options.compilerArgs += ["-Xlint:all", "-Xlint:-processing", "-Xlint:-path", "-Xlint:-serial"]
    
    // Configure logging for compilation warnings
    tasks.withType(JavaCompile).configureEach {
        doLast {
            if (logging.level == LogLevel.INFO || logging.level == LogLevel.DEBUG) {
                println "Compilation warnings for ${project.name}:"
                println "Source: ${sourceCompatibility}, Target: ${targetCompatibility}"
            }
        }
    }

    tasks.withType(AbstractArchiveTask) {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn(subprojects.test)

    jacocoClasspath = project(':core').configurations.jacocoAnt
    additionalSourceDirs.from(files(subprojects.java.sourceSets.main.allSource.srcDirs))
    sourceDirectories.from(files(subprojects.java.sourceSets.main.allSource.srcDirs))
    classDirectories.from(files(subprojects.java.sourceSets.main.output))
    executionData.from(files(subprojects.jacocoTestReport.executionData))

    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }

    // > The value for this file collection is final and cannot be changed.
    //doFirst {
    //    executionData = files(executionData.findAll { it.exists() })
    //}

    reports {
        // https://stackoverflow.com/questions/73546718/changing-enabled-to-required-throws-an-error-in-gradle
        xml.required.set(true)
        xml.outputLocation.set(file("${buildDir}/reports/jacoco/report.xml"))
        html.required.set(true)
        html.outputLocation.set(file("${buildDir}/reports/jacoco/html"))
    }
}

apply from: file('gradle/update.gradle')
